@namespace Lipeg.Boot
@classname LipegParser
@using Pegasus.Parser
@using Lipeg.SDK.Tree
@using Lipeg.Runtime;
@using System.Globalization
@using System.Linq
@using System.Text.RegularExpressions

grammar <INode>
    = _ a:'grammar' _ i:identifier '{' _ c:content* e:'}' _ EOF { NE(aStart, eEnd, "grammar", i, NS(cStart, cEnd, c)) }

content <INode>
    = options
    / rules
    / lexical

options <INode>
    = a:'options' _ '{' _ s:option<0,,_> o:'}' _ { NE(aStart, oEnd, "options", NS(sStart, sEnd, s)) }

option <INode>
    = id:identifier '=' _ value:optionValue o:(';' _)? { N(idStart, oEnd, "option", id, value) }

optionValue <INode>
    = qualifiedIdentifier

qualifiedIdentifier <INode>
    = id:identifier<1,,'.' _> { NE(idStart, idEnd, "qualifiedIdentifier", NP(idStart, idEnd, id)) }

rules <INode>
    = a:'rules' _ '{' _  r:rule<0,,_> o:'}' _ { NE(aStart, oEnd, "rules",  NS(rStart, rEnd, r)) }

lexical <INode>
    = a:'lexical' _ '{' _  r:rule<0,,_> o:'}' _ { NE(aStart, oEnd, "lexical",  NS(rStart, rEnd, r)) }

rule <INode>
    = a:identifier '<=' _ e:topChoice o:';' _ { N(aStart, oEnd, "rule", a, e) }

expression <INode>
    = choice

topChoice <INode>
    = ('/' _)? c:sequence<1,,_ '/' _>  { NE(cStart, cEnd, "choice", NP(cStart, cEnd, c)) }

choice <INode>
    = c:sequence<1,,_ '/' _>  { NE(cStart, cEnd, "choice", NP(cStart, cEnd, c)) }

sequence <INode>
    = e:prefix<0,,_> { NE(eStart, eEnd, "sequence", NS(eStart, eEnd, e)) }

prefix <INode>
    = a:'&' _ e:suffix { N(aStart, eEnd, "and", e) }
    / a:'!' _ e:suffix { N(aStart, eEnd, "not", e) }
    / a:'^' _ e:suffix { N(aStart, eEnd, "promote", e) }
    / a:':' _ e:suffix { N(aStart, eEnd, "drop", e) }
    / a:'~' _ e:suffix { N(aStart, eEnd, "fuse", e) }
    / suffix

suffix <INode>
    = e:primary _ q:quantifier { N(eStart, qEnd, "quantified", e, q) }
    / primary

primary <INode> -memoize
    = identifier
    / alias
    / stringLiteral
    / class
    / dot
    / '(' _ expression:expression ')' _ { expression }

quantifier <INode> -memoize
    = q:'?' _                                                                       { N(qStart, qEnd, "?") }
    / q:'*' _                                                                       { N(qStart, qEnd, "*") }
    / q:'+' _                                                                       { N(qStart, qEnd, "+") }

dot <INode>
    = q:'.' _ { N(qStart, qEnd, ".") }


integer <INode> -memoize
    = d:digits _ { N(dStart, dEnd, "integer", Flat(d)) }

digits
    = digit+

identifier <INode> -memoize
    = name:(!digit (letter / digit / "_" / "$")+) _ { N(nameStart, nameEnd, "identifier", name) }

stringLiteral <INode>
    = singleQuotedString

alias <INode>
    = doubleQuotedString

doubleQuotedString <INode>
  = a:'"' c:(doubleQuotedCharacter*) o:'"' _ { N(aStart, oEnd, "doubleString", NS(cStart, cEnd, c)) }

singleQuotedString <INode>
    = a:'\'' c:(singleQuotedCharacter*) o:'\'' _ { NE(aStart, oEnd, "singleString", NS(cStart, cEnd, c)) }

class <INode>
    = a:'[' inv:'^'? p:(classCharacterRange / classCharacter)* o:']' _ { N(aStart, oEnd, "class", N(invStart, invEnd, "invert", NO(inv)), NS(pStart, pEnd, p)) }

doubleQuotedCharacter <INode>
    = simpleDoubleQuotedCharacter
    / simpleEscapeSequence
    / hexEscapeSequence
    / unicodeEscapeSequence

singleQuotedCharacter <INode>
    = simpleSingleQuotedCharacter
    / simpleEscapeSequence
    / hexEscapeSequence
    / unicodeEscapeSequence

bracketDelimitedCharacter <INode>
    = simpleBracketDelimitedCharacter
    / simpleEscapeSequence
    / hexEscapeSequence
    / unicodeEscapeSequence

classCharacterRange <INode>
    = a:bracketDelimitedCharacter '-' o:bracketDelimitedCharacter { N(aStart, oEnd, "range", a, o) }

classCharacter <INode>
    = c:bracketDelimitedCharacter { N(cStart, cEnd, "single", c) }

simpleDoubleQuotedCharacter <INode>
    = !('"' / "\\" / eolChar) c:. { N(cStart, cEnd, "character", c) }

simpleSingleQuotedCharacter <INode>
    = !("'" / "\\" / eolChar) c:. { N(cStart, cEnd, "character", c) }

simpleBracketDelimitedCharacter <INode>
    = !(']' / '\\' / eolChar) c:. { N(cStart, cEnd, "character", c) }

simpleEscapeSequence <INode>
    = "\\" !("x" / "u" / eolChar) c:.   { N(cStart, cEnd, "simpleEscape", c) }

hexEscapeSequence <INode>
    = a:"\\x" d:(hexDigit<2,2>) { N(aStart, dEnd, "hexEscape", Flat(d)) }

unicodeEscapeSequence <INode>
    = a:'\\u{' d:(hexDigit<1,6>) o:'}' { N(aStart, oEnd, "unicodeEscape", Flat(d)) }

digit
    = [0-9]

hexDigit
    = [0-9a-fA-F]

letter
    = [a-zA-Z]

_ -memoize
    = (whitespace / newline / comment)*

comment -memoize
    = singleLineComment
    / multiLineComment

singleLineComment -memoize
    = "//" (!eolChar .)*

multiLineComment
    = "/*" (!"*/" .)* "*/"

newline -memoize
    = "\n"
    / "\r\n"
    / "\r"
    / "\u2028"
    / "\u2029"

eolChar
    = [\n\r\u2028\u2029]

whitespace -memoize
    = [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]

EOF
    = !.
    / unexpected:&. #error{ "PEG0008:" + string.Format(CultureInfo.CurrentCulture, XyzResources.PEG0008_ERROR_PARSER_UnexpectedEndOfInput, unexpected) }
