grammar lipeg
{
    options
    {
        start = start;
        spacing = _;
    }
    syntax
    {
        start <=
            grammar eof
            ;
        
        grammar <=
            ,'grammar' identifier ,'{' ^(options / syntax / lexical)* ,'}'
            ;
        
        options <=
            ,'options' ,'{' ^option* ,'}'
            ;
        
        option <=
            identifier ,'=' option-value ,';'
            ;
        
        option-value <=
            qualified-identifier
            ;
        
        qualified-identifier <=
            identifier ^(,'.' identifier)*
            ;
        
        syntax <=
            ,'syntax' ,'{' ^rules ,';'? ,'}'
            ;
        
        lexical <=
            ,'lexical' ,'{' ^rules ,';'? ,'}'
            ;
        
        rules <=
            ^(rule ^(,';' rule)*)?
            ;
        
        rule <=
            identifier ,'<=' ,'/'? ^expression
            ;
        
        expression <=
            / choice
            / sequence
            ;
        
        choice <=
            sequence ^(,'/' sequence)+
            ;
        
        sequence <=
            ^(^prefix)+
            ;
        
        prefix <=
            / (and <=
                (,'&' ^suffix))
            / (not <=
                (,'!' ^suffix))
            / (drop <=
                (,',' ^suffix))
            / (fuse <=
                (,'~' ^suffix))
            / (lift <=
                (,'^' ^suffix))
            / ^suffix
            ;
        
        suffix <=
            / (zero-or-one <=
                (^primary ,'?'))
            / (zero-or-more <=
                (^primary ,'*'))
            / (one-or-more <=
                (^primary ,'+'))
            / ^primary
            ;
        
        primary <=
            / identifier
            / verbatim-string
            / string
            / character-class
            / any
            / epsilon
            / inline
            / (,'(' ^expression ,')')
            ;
        
        inline <=
            ,'(' ^rule ,')'
            ;
        
        any <=
            ,'.'
            ;
        
        epsilon <=
            / 'epsilon'
            / 'Îµ'
            ;
        
        eof <=
            !.
            ;
    }
    lexical
    {
        identifier <=
            / ~(letter ^letter-or-digit* ^('-' ^letter-or-digit+)*)
            / ~'_'
            ;
        
        letter <=
            [a-zA-Z]
            ;
        
        letter-or-digit <=
            [a-zA-Z0-9]
            ;
        
        hex-digit <=
            [0-9a-fA-F]
            ;
        
        verbatim-string <=
            ~(,'\'' ^verbatim-character* ,'\'')
            ;
        
        verbatim-character <=
            !('\'' / '\\' / eol-char) .
            ;
        
        string <=
            ,'\'' ^(^character)* ,'\''
            ;
        
        character <=
            / string-verbatim
            / escape
            / unicode-escape
            / byte-escape
            ;
        
        string-verbatim <=
            ~(!('\'' / '\\' / eol-char) .)
            ;
        
        escape <=
            ~(,'\\' [0abefnrtv\-\]\\\'])
            ;
        
        unicode-escape <=
            ,('\\u{' _) unicode-number ,_ ^(unicode-number ,_)* ,'}'
            ;
        
        unicode-number <=
            ~(hex-digit ^hex-digit? ^hex-digit? ^hex-digit? ^hex-digit? ^hex-digit?)
            ;
        
        byte-escape <=
            ,('\\x{' _) byte-number ,_ ^(byte-number ,_)* ,'}'
            ;
        
        byte-number <=
            hex-digit ^hex-digit?
            ;
        
        character-class <=
            ,'[' not ^(^class-part)* ,']'
            ;
        
        not <=
            ^'^'?
            ;
        
        class-part <=
            / range
            / ^class-char
            ;
        
        range <=
            ^class-char ,'-' ^class-char
            ;
        
        class-char <=
            / class-verbatim
            / escape
            / unicode-escape
            / byte-escape
            ;
        
        class-verbatim <=
            ~(!(']' / '\\' / eol-char) .)
            ;
        
        _ <=
            (whitespace / newline / comment)*
            ;
        
        comment <=
            / single-line-comment
            / multi-line-comment
            ;
        
        single-line-comment <=
            '//' (!eol-char .)*
            ;
        
        multi-line-comment <=
            '/*' (!'*/' .)* '*/'
            ;
        
        newline <=
            / '\r\n'
            / [\n\r\u{2028}\u{2029}]
            ;
        
        eol-char <=
            [\n\r\u{2028}\u{2029}]
            ;
        
        whitespace <=
            [ \t\v\f\u{00A0}\u{FEFF}\u{1680}\u{180E}\u{2000}-\u{200A}\u{202F}\u{205F}\u{3000}]
            ;
    }
}
